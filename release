#!/usr/bin/perl

use strict;
use warnings;

use File::Copy;
use File::Path;
use Template;
use YAML 'LoadFile';
use constant TT   => '/home/dave/src/release';

my $USER = 'mag-sol@mag-sol.com';
my $DIR  = 'subdomains/code/httpdocs';

die "Can't find META.yml\n" unless -f 'META.yml';
die "META.yml is empty\n"   if -z 'META.yml';

my $meta = LoadFile('META.yml');

my $build;
if (-f 'Build.PL') {
  $build = { start => './Build.PL', cmd => './Build' };
} else {
  $build = { start => './Makefile.PL', cmd => 'make' };
}

system "perl $build->{start}";
system "cover -delete";
system "HARNESS_PERL_SWITCHES=-MDevel::Cover $build->{cmd} test";
system "cover report html_basic";

rmtree($meta->{name}) if -d $meta->{name};

mkdir "$meta->{name}";
mkdir "$meta->{name}/cover";
foreach (<cover_db/*.html>, <cover_db/*.css>) {
  s|^cover_db/||;
  copy "cover_db/$_", "$meta->{name}/cover/$_";
}
move "$meta->{name}/cover/coverage.html", "$meta->{name}/cover/index.html";

my $rpm = -e "perl-$meta->{name}-$meta->{version}-1.noarch.rpm";

my $t = Template->new({INCLUDE_PATH => TT});
$t->process('index.tt', { rpm => $rpm }, "$meta->{name}/index.html")
  or die $t->error;

my $ver = "$meta->{name}-$meta->{version}";
copy "$ver.tar.gz", $meta->{name};
if ($rpm) {
  copy $_, $meta->{name} for <perl-*.rpm>
}
chdir "$meta->{name}";
system "zcat $ver.tar.gz | tar xvf -";
move $ver, 'dist';
chdir '..';
system "ssh $USER rm -rf $DIR/$meta->{name}";
system "scp -r ./$meta->{name} $USER:$DIR/";
